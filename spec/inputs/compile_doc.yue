outputFolder = ...
for [compiledFile, docFile] in *[
		["codes_from_doc.lua", "doc/docs/doc/index.md"]
		["codes_from_doc_zh.lua", "doc/docs/zh/doc/index.md"]
	]
	close input = with? io.open docFile
		import "yue" as :to_lua
		text = \read "*a"
		codes = []
		for code in text\gmatch "```moonscript(.-)```"
			if result, err := to_lua code, implicit_return_root: false, reserve_line_number: false
				codes[] = result
			elseif not err\match "macro exporting module only accepts macro definition"
				print err
				os.exit 1
		for code in text\gmatch "```yue(.-)```"
			if result, err := to_lua code, implicit_return_root: false, reserve_line_number: false
				codes[] = result
			else
				print err
				os.exit 1
		close output = with io.open "#{outputFolder}/#{compiledFile}", "w+"
			\write table.concat codes

